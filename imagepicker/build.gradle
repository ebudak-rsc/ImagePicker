apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply from: "../ktlint.gradle"

final Properties githubProperties = new Properties()
githubProperties.load(new FileInputStream('ps-github.properties'))

android {
    compileSdkVersion 35
    defaultConfig {
        multiDexEnabled true
        minSdkVersion 23
        targetSdkVersion 35

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    namespace 'com.github.drjacky.imagepicker'
    buildFeatures {
        viewBinding true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "net.petsafe"
            artifactId "image-picker"
            version "2.4.0"
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")

            pom.withXml {
                def root = it.asNode()
                root.with {
                    appendNode('name', 'Petsafe Image Picker for Android')
                    appendNode('description', 'Library to handle picking images')
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('url', 'https://www.mit.edu/~amini/LICENSE.md')
                        }
                    }
                    appendNode('organization').with {
                        appendNode('name', 'Radio Systems Corporation')
                        appendNode('url', 'https://petsafe.net')
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', 'fperez-rsc')
                            appendNode('name', 'Fernando Perez')
                            appendNode('email', 'fperez@petsafe.net')
                            appendNode('organization', 'Radio Systems Corporation')
                            appendNode('organizationUrl', 'https://petsafe.net')
                        }
                    }
                }
                def dependenciesNode = root.appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name "GitHubPackages"
            url = uri("https://maven.pkg.github.com/RSC-IoT/ps-maven-repo")
            credentials {
                username githubProperties['psgh.usr']
                password githubProperties['psgh.key']
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.exifinterface:exifinterface:1.4.2'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.10.0'
    implementation 'androidx.fragment:fragment-ktx:1.8.9'

    //More Info: https://github.com/Yalantis/uCrop
    implementation 'com.github.yalantis:ucrop:2.2.11'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}
